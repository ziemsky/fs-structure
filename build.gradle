
buildscript {
    repositories {
        mavenLocal()
    }
}

plugins {
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.3.0'

    id 'idea'
}

group 'com.ziemsky.fsstructure'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    groovyVersion = '2.4.13'
    spockVersion = '1.1-groovy-2.4-rc-4'
}

subprojects {
    version = rootProject.version
}

repositories {
    mavenCentral()
}

sourceSets {
    testUtils { // common test resources used by all other test sourceSets
        compileClasspath += sourceSets.main.output.classesDirs
    }

    integration

    functional
}

// Fixes problem of disappearing designations of test sources roots in custom sourceSets, following
// https://youtrack.jetbrains.com/issue/IDEA-151925#comment=27-2355076
idea {
    module {
        testSourceDirs += project.sourceSets.integration.groovy.srcDirs \
                        + project.sourceSets.integration.resources.srcDirs \
                        + project.sourceSets.functional.groovy.srcDirs \
                        + project.sourceSets.functional.resources.srcDirs
    }
}
idea.module.testSourceDirs.containsAll(sourceSets.integration.allJava.srcDirs)
idea.module.testSourceDirs.containsAll(sourceSets.functional.allJava.srcDirs)

dependencies {
    compile("commons-io:commons-io:2.5")
    compile("org.apache.commons:commons-lang3:3.8.1")

    testUtilsCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")

    testCompile sourceSets.testUtils.output
    testCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    testCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude module: 'groovy-all'
    }
    testCompile('org.apache.commons:commons-lang3:3.5')
    testRuntime('net.bytebuddy:byte-buddy:1.8.0')
    testRuntime('org.objenesis:objenesis:2.6')

    integrationCompile sourceSets.main.output
    integrationCompile sourceSets.testUtils.output
    integrationCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    integrationCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude module: 'groovy-all'
    }

    functionalCompile sourceSets.main.output
    functionalCompile sourceSets.testUtils.output
    functionalCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    functionalCompile("org.spockframework:spock-core:${spockVersion}") {
        exclude module: 'groovy-all'
    }
}


task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath
}

integrationTest.mustRunAfter(test)
functionalTest.mustRunAfter(test, integrationTest)
check.dependsOn(test, integrationTest, functionalTest, jacocoTestCoverageVerification, 'pitest')
jacocoTestCoverageVerification.mustRunAfter tasks.findAll { it.name.toLowerCase().endsWith('test') && !it.name.toLowerCase().equalsIgnoreCase('pitest')}
tasks.getByName('pitest').mustRunAfter jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)


defaultTasks 'keyTasks'

task keyTasks {

    group 'help'

    description 'Prints out the most often used tasks in daily development and other basic build info.'

    doLast {
        logger.quiet('''
                     |============================================================================
                     |
                     |    Project: {}
                     |    
                     |             {}
                     |
                     | Key tasks:
                     |
                     |    check               - Runs all tests - unit, integration, and acceptance.
                     |    test                - Runs unit tests.
                     |    integrationTest     - Runs integration tests.
                     |    functionalTest      - Runs acceptance tests.
                     |    
                     |    publishToMavenLocal - Packages and publishes the plugin to local Maven repository. 
                     |    
                     |    tasks               - All tasks available in this build.
                     |    help                - Gradle help.
                     |
                     |    To execute any of the above TASKs, navigate to the root directory of this project and execute:
                     |
                     |                   ./gradlew TASK
                     |
                     |    To attach debugger to running tests, add '-Dtest.debug' when executing a test task and connect debugger
                     |    to port 5005 - Gradle will pause execution of tests until you connect.
                     |    
                     |    See readme.md for more details.
                     |
                     |============================================================================
                     '''.stripMargin(),
                project.name,
                project.description ? project.description : ""
        )
    }
}
keyTasks.dependsOn help

//
// Releasing
//

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java

            groupId     project.group
            artifactId  'fs-structure'
            version     project.version
        }
    }
}

tag {
    message {
        "version: ${project.version}"
    }
}

//
// JaCoCo - code coverage
//

jacocoTestCoverageVerification {
    violationRules {
        // See http://www.jacoco.org/jacoco/trunk/doc/ant.html
        //     https://docs.gradle.org/current/javadoc/org/gradle/testing/jacoco/tasks/rules/JacocoViolationRule.html
        //     http://www.jacoco.org/jacoco/trunk/doc/counters.html

        rule {
            includes = ['com.ziemsky.fsstructure.*']
            excludes = ['*__spock_*', 'com.ziemsky.fsstructure.test.*']

            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }

        rule {
            // No more than 5 logical paths through a method -> no more than 5 tests required to test the method
            // See http://www.jacoco.org/jacoco/trunk/doc/counters.html

            includes = ['com.ziemsky.fsstructure.*']
            excludes = ['*__spock_*', 'com.ziemsky.fsstructure.test.*']

            element = 'METHOD'

            limit {
                counter = 'COMPLEXITY'
                value = 'TOTALCOUNT'
                maximum = 5
            }
        }
    }
}

test {
    jacoco {
        includes = ['com.ziemsky.fsstructure.*']
        excludes = ['*__spock_*', 'com.ziemsky.fsstructure.test.*']

        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

integrationTest {
    jacoco {
        includes = ['com.ziemsky.fsstructure.*']
        excludes = ['*__spock_*', 'com.ziemsky.fsstructure.test.*']

        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

functionalTest {
    jacoco {
        includes = ['com.ziemsky.fsstructure.*']
        excludes = ['*__spock_*', 'com.ziemsky.fsstructure.test.*']

        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

//
// PIT test (mutation checks)
//

pitest {
    coverageThreshold = 100
    mutationThreshold = 100
    outputFormats = ['HTML']
    testSourceSets = [sourceSets.test, sourceSets.integration, sourceSets.functional]
}